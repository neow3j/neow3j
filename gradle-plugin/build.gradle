plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.2.1'
}

description = "neow3j: Gradle plugin to compile Java smart contracts to NeoVM"

gradlePlugin {
    website = "https://github.com/neow3j/neow3j"
    vcsUrl = "https://github.com/neow3j/neow3j"

    plugins {
        neow3jDevpackGradlePlugin {
            id = "io.neow3j.gradle-plugin"
            implementationClass = "io.neow3j.devpack.gradle.Neow3jPlugin"
            displayName = 'Gradle plugin for neow3j compiler'
            description = "Gradle plugin to compile Java smart contracts to NeoVM"
            tags = ["javac", "neo", "blockchain", "neow3j", "java"]
        }
    }
}

// from:
// https://docs.gradle.org/6.8.1/userguide/test_kit.html#sec:working_with_gradle_versions_prior_to_213
task createClasspathManifest {
    def outputDir = file("$buildDir/$name")

    inputs.files(sourceSets.main.runtimeClasspath)
            .withPropertyName("runtimeClasspath")
            .withNormalizer(ClasspathNormalizer)
    outputs.dir(outputDir)
            .withPropertyName("outputDir")

    doLast {
        outputDir.mkdirs()
        file("$outputDir/plugin-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
    }
}

dependencies {
    implementation project(':compiler'),
            project(':contract'),
            gradleApi()

    testImplementation gradleTestKit()

    testRuntimeOnly files(createClasspathManifest)
}